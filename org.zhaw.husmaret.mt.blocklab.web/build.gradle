plugins {
	id 'war'
	id "com.github.node-gradle.node" version "3.2.1"
}
dependencies {
	api project(':org.zhaw.husmaret.mt.blocklab')
	api project(':org.zhaw.husmaret.mt.blocklab.ide')
	api "org.eclipse.xtext:org.eclipse.xtext.xbase.web:${xtextVersion}"
	api "org.eclipse.xtext:org.eclipse.xtext.web.servlet:${xtextVersion}"
	api "org.eclipse.xtend:org.eclipse.xtend.lib:${xtextVersion}"
	api "org.webjars:requirejs:2.3.6"
	api "org.webjars:jquery:3.6.0"
	api "org.webjars:ace:1.3.3"
	api "org.webjars.npm:google-closure-library:20210808.0.0"
	// https://mvnrepository.com/artifact/org.eclipse.jetty/jetty-util
	implementation 'org.eclipse.jetty:jetty-util:9.4.53.v20231009'

	// api 'org.eclipse.jetty:jetty-client:9.4.53.v20231009'
	//api "org.eclipse.jetty:apache-jsp:9.3.4.RC1"

	providedCompile 'org.eclipse.jetty:jetty-annotations:9.4.53.v20231009'
	providedCompile "org.slf4j:slf4j-simple:1.7.33"

}

sourceSets {
  main {
    java.srcDirs += 'src/main/xtend-gen'
  }
}

version '1.0.0'

node {
  version = '18.16.1'
  npmVersion = '9.7.2'
  download = true
  workDir = file("${project.buildDir}/node")
  nodeModulesDir = file("${project.projectDir}/src/main/webapp")
}

task deleteStaticFolder(type: Delete) {
    def dirName = "src/main/webapp/blocklabIDE" 
    file( dirName ).list().each{
        f -> 
            delete "${dirName}/${f}"
    }
	delete "${dirName}"
}

// check here for arguments
npm_install.args = ['--force']

task buildNpm(type: NpmTask) {
  args = ['run', 'build']
}

buildNpm.dependsOn(npm_install, deleteStaticFolder)

task jettyRun(type:JavaExec) {
	String defaultPort = '8080'
	dependsOn(sourceSets.main.runtimeClasspath, buildNpm)
	classpath = sourceSets.main.runtimeClasspath.filter{it.exists()}
	mainClass = 'org.zhaw.husmaret.mt.web.ServerLauncher'
	args defaultPort 
	standardInput = System.in
	group = 'run'
	description = 'Starts a Jetty server with the BlockLab DSL'
}

tasks.war {
	dependsOn(buildNpm)
	exclude('node_modules/**')
	exclude('src/**')
	exclude('package-lock.json')
	exclude('package.json')
	setProperty("zip64", true)
}
